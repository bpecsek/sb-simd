(in-package #:sb-vm)

(defknown sb-simd-internals::%cpuid ((unsigned-byte 32) (unsigned-byte 32))
    (values (unsigned-byte 32) (unsigned-byte 32)
            (unsigned-byte 32) (unsigned-byte 32))
    (always-translatable)
  :overwrite-fndb-silently t)

(define-vop (sb-simd-internals::%cpuid)
  (:policy :fast-safe)
  (:translate sb-simd-internals::%cpuid)
  (:args (rax-val :scs (unsigned-reg) :target rax)
         (rcx-val :scs (unsigned-reg) :target rcx))
  (:arg-types unsigned-num unsigned-num)
  (:temporary (:sc unsigned-reg :offset rax-offset :target r1 :from (:argument 0)) rax)
  (:temporary (:sc unsigned-reg :offset rcx-offset :target r3 :from (:argument 1)) rcx)
  (:temporary (:sc unsigned-reg :offset rbx-offset :target r2) rbx)
  (:temporary (:sc unsigned-reg :offset rdx-offset :target r4) rdx)
  (:results
   (r1 :scs (unsigned-reg))
   (r2 :scs (unsigned-reg))
   (r3 :scs (unsigned-reg))
   (r4 :scs (unsigned-reg)))
  (:result-types unsigned-num unsigned-num unsigned-num unsigned-num)
  (:generator 8
   (move rax rax-val)
   (move rcx rcx-val)
   (inst cpuid)
   (move r1 rax)
   (move r2 rbx)
   (move r3 rcx)
   (move r4 rdx)))
